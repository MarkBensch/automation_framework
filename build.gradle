
group 'com.example'
version '0.1-SNAPSHOT'

import org.gradle.internal.os.OperatingSystem
apply plugin: 'groovy'



ext {
	groovyVersion = '2.4.11'
    driverVersion = '3.5.3'
    gechoVersion = "v0.18.0"
    log4jVersion = '2.8.2'
	spockReportVersion = '1.2.12'
	spockVersion = '1.1-groovy-2.4-rc-2'
	gebVersion = '1.1.1'
	htmlUnitDriverVersion = '2.27'
	appiumDriverVersion = '5.0.2'
}

repositories {
    mavenCentral()
}

dependencies {

// Language
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"

// Fancy Reports
    testCompile( "com.athaydes:spock-reports:$spockReportVersion" ) {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }

// Logger
    testCompile 'org.slf4j:slf4j-simple:1.7.13'
    testCompile "org.apache.logging.log4j:log4j-core:$log4jVersion"
    testCompile "org.apache.logging.log4j:log4j-api:$log4jVersion"

// Test Runners
    testCompile "junit:junit:4.11"
    testCompile "org.spockframework:spock-core:$spockVersion"

// API Frameworks
    testCompile "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1"

// webdriver framework
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$driverVersion"
	testCompile "org.seleniumhq.selenium:selenium-safari-driver:$driverVersion"
	testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$driverVersion"
	testCompile "org.seleniumhq.selenium:htmlunit-driver:$htmlUnitDriverVersion"
	testCompile "io.appium:java-client:$appiumDriverVersion"
}

//run tests with JUnit categories
task fastTest(type: Test){
    useJUnit {
        includeCategories 'package.name.of.class.FastTests'
    }
}
task unzipGeckoDriver(type: Copy) {
	//https://github.com/mozilla/geckodriver/releases
	def outputDir = file("$buildDir/webdriver/geckodriver")
	outputs.dir(outputDir)
	if( OperatingSystem.current().isWindows()){
		from(zipTree("drivers/geckodriver-${gechoVersion}-win64.zip"))
	} else {
		from(tarTree("drivers/geckodriver-${gechoVersion}-macos.tar.gz"))
	}
	into(outputDir)
}


task unzipChromeDriver(type: Copy) {
		//https://sites.google.com/a/chromium.org/chromedriver/downloads
		def outputDir = file("$buildDir/webdriver/chromedriver")
		outputs.dir(outputDir)
		if( OperatingSystem.current().isWindows()){
			from(zipTree("drivers/chromedriver_win32.zip"))
		} else {
			from(zipTree("drivers/chromedriver_mac64.zip"))
		}
		into(outputDir)
}

//modify all testing tasks
tasks.withType(Test) {
    testLogging {
        events 'started', 'passed'
    }
	reports {
		html.destination = reporting.file("$name/tests")
		junitXml.destination = file("$buildDir/test-results/$name")
	}
	println name
	systemProperty("geb.env", "HtmlUnit")
	outputs.upToDateWhen { false }
    //maxParallelForks = 2
    //forkEvery = 2
}

task firefoxTest(type: Test) {
    dependsOn unzipGeckoDriver
    def geckodriverFilename = OperatingSystem.current().isWindows() ? "geckodriver.exe" : "geckodriver"
    def geckodriverFile = new File(unzipGeckoDriver.outputs.files.singleFile, geckodriverFilename)
    systemProperty("webdriver.gecko.driver", geckodriverFile.absolutePath)
	systemProperty("geb.env", "Firefox")
}

task safariTest(type: Test) {
	systemProperty("geb.env", "Safari")
}

task chromeTest(type: Test) {
	dependsOn unzipChromeDriver
	def chromedriverFilename = OperatingSystem.current().isWindows() ? "chromedriver.exe" : "chromedriver"
	def chromedriverFile = new File(unzipChromeDriver.outputs.files.singleFile, chromedriverFilename)
	systemProperty("webdriver.chrome.driver", chromedriverFile.absolutePath)
	systemProperty("geb.env", "Chrome")
}

task saucelabTest(type: Test){
	systemProperty("geb.env", "SauceLab")
	//TODO: Come back and do this right
	// <<browser>>:<<os>>:<<version>>
	systemProperty("geb.remoteServer", "chrome:52.0:Windows 10")

}

task saucelabMobileTest(type: Test){
	systemProperty("geb.env", "MobileSauceLab")
	//TODO: Come back and do this right
	// <<platformName>>:<<platformVersion>>:<<browserName>>:<<deviceOrientation>>:<<deviceName>>:<<appiumVersion>>
	//systemProperty("geb.remoteMobile", "Android:7.1:Chrome:portrait:Android GoogleAPI Emulator:1.6.5")
	systemProperty("geb.remoteMobile", "iOS:10.3:Safari:portrait:iPhone 7 Simulator:1.6.5")


}
task browserstackTest(type: Test){
	systemProperty("geb.env", "BrowserStack")
	//TODO: Come back and do this right
	// <<browser>>:<<os>>:<<version>>
	systemProperty("geb.remoteServer", "chrome:52.0:Windows")
}

task browserstackMobileTest(type: Test){
	systemProperty("geb.env", "MobileBrowserStack")
	//TODO: Come back and do this right
	// <<os>>:<<os_version>>:<<browser>>:<<deviceOrientation>>:<<deviceName>>:<<appiumVersion>>:<<realMobile>>
	//systemProperty("geb.remoteMobile", "android:8.0:Android:portrait:Google Pixel:1.4.16:true")
	systemProperty("geb.remoteMobile", "ios:10.0:iPhone:portrait:iPhone 7:1.6.5:true")
}

